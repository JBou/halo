apply plugin: 'base'
apply plugin: 'eclipse'
apply plugin: 'com.bmuschko.nexus'
apply from: 'gradle/eclipse-prefs/eclipse.gradle'

buildscript {
    repositories {
        mavenCentral()
    }
    
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'jacoco'
jacoco { toolVersion = '0.8.5' }

apply plugin: 'java-library'
apply plugin: 'eclipse'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    testImplementation(
        'io.cucumber:cucumber-java:5.6.0',
        'io.cucumber:cucumber-junit:5.6.0',
        'io.cucumber:cucumber-picocontainer:5.6.0',
        'org.junit.vintage:junit-vintage-engine:5.6.1',
        'org.awaitility:awaitility:4.0.2',
        'org.jmdns:jmdns:3.5.5',
        'org.slf4j:slf4j-jdk14:1.7.30'
    )
}

test {
    testLogging.showStandardStreams = true
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

if (Integer.valueOf(JavaVersion.current().getMajorVersion()) > 8) {
    // --release 8: configures the compiler to produce class files that will link against version 8
    compileJava.options.compilerArgs += ['--release', 8]
}

compileJava.options.encoding = 'UTF-8'
compileJava.options.compilerArgs += [
    '-Xlint:all',
    '-Werror'
]

jar {
    /* include source */
    from (sourceSets.main.allSource.getSrcDirTrees())
}

javadoc {
   options.memberLevel = JavadocMemberLevel.PUBLIC
}

/* Modify the Eclipse project settings. */
eclipse { jdt { file { withProperties eclipsePrefs } } }

task ('copyEclipsePrefs', type: Copy) {
    from rootProject.file('gradle/eclipse-prefs/org.eclipse.jdt.ui.prefs')
    into "${projectDir}/.settings"
}
tasks.eclipse.finalizedBy copyEclipsePrefs

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

modifyPom {
    project {
        name 'halo'
        description 'An implementation of Multicast DNS Service Discovery in Java.'
        url 'https://github.com/ofmooseandmen/halo'
        inceptionYear '2020'

        scm {
            url 'https://github.com/ofmooseandmen/halo'
            connection 'scm:https://ofmooseandmen@github.com/ofmooseandmen/halo.git'
            developerConnection 'scm:git://github.com/ofmooseandmen/halo.git'
        }

        licenses {
            license {
                name 'The 3-Clause BSD License'
                url 'https://opensource.org/licenses/BSD-3-Clause'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'ofmooseandmen'
                name 'Cedric Liegeois'
                email 'ofmooseandmen@yahoo.com'
            }
        }
    }
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

